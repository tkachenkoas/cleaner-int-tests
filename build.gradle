plugins {
    id 'java'
    id 'groovy'

    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'

    id 'org.springdoc.openapi-gradle-plugin' version '1.6.0'
    id "org.openapi.generator" version "6.3.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.apache.httpcomponents:httpclient'

    implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'

    runtimeOnly 'com.h2database:h2'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // for compilation of generated code
    testCompileOnly 'org.openapitools:jackson-databind-nullable:0.2.6'
    testCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    testCompileOnly "io.swagger:swagger-annotations:1.6.9"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.13'
}

// https://github.com/springdoc/springdoc-openapi-gradle-plugin
openApi {
    apiDocsUrl.set("http://localhost:8080/v3/api-docs.yaml")
    outputDir.set(file("./"))
    outputFileName.set("api-schema.yaml")
}

task generateGroovyModels(
        type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask,
        group: 'openapi tools'
){
    generatorName = "groovy"
    doLast {
        delete "build/generated/src/main/groovy/com/example/apiclient/api"
    }
}

task generateJavaInvoker(
        type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask,
        group: 'openapi tools'
){
    generatorName = "java"
    library = 'resttemplate'
    doLast {
        delete "build/generated/src/main/java/com/example/apiclient/model"
        copy {
            from 'build/generated/src/main/java'
            into "build/generated/src/main/groovy"
        }
    }
}

[generateGroovyModels, generateJavaInvoker].each {
    it.inputSpec = "$rootDir/api-schema.yaml"
    it.outputDir = "$buildDir/generated"
    it.apiPackage = "com.example.apiclient.api"
    it.invokerPackage = "com.example.apiclient.invoker"
    it.modelPackage = "com.example.apiclient.model"
}

sourceSets.test.groovy.srcDirs += [
        'build/generated/src/main/groovy'
]

compileTestGroovy.dependsOn generateJavaInvoker, generateGroovyModels

tasks.named('test') {
    useJUnitPlatform()
}
